dnl Process this file with autoconf to produce a configure script.

dnl %CopyrightBegin%
dnl
dnl Copyright Basho Technologies, Inc. 2015. All Rights Reserved.
dnl Copyright Ericsson AB 1998-2014. All Rights Reserved.
dnl
dnl The contents of this file are subject to the Erlang Public License,
dnl Version 1.1, (the "License"); you may not use this file except in
dnl compliance with the License. You should have received a copy of the
dnl Erlang Public License along with this software. If not, it can be
dnl retrieved online at http://www.erlang.org/.
dnl
dnl Software distributed under the License is distributed on an "AS IS"
dnl basis, WITHOUT WARRANTY OF ANY KIND, either express or implied. See
dnl the License for the specific language governing rights and limitations
dnl under the License.
dnl
dnl %CopyrightEnd%

AC_PREREQ(2.59)

AC_INIT()

LM_PRECIOUS_VARS

dnl
dnl Don't use the AC_MSG_xxx macros yet, m4 *may* barf this early
dnl
# The no_recursion variable is not documented, but the only
# action we take on it is disabling caching which is safe!
if test "x$no_recursion" != xyes \
    -a "x$OVERRIDE_CONFIG_CACHE" = x \
    -a "x$cache_file" != x/dev/null
then
  if test "x$cache_file" != x
  then
    case "$cache_file" in
      config.cache | ./config.cache | */dev/null )
        ;;
      * )
        echo Ignoring cache since it can cause the system to be erroneously configured
        ;;
    esac
    if test -f "$cache_file"
    then
      echo Removing cache file $cache_file
      rm -f "$cache_file"
    fi
  fi
  echo Disabling caching
  cache_file=/dev/null
fi

AC_MSG_CHECKING(for Erlang/OTP source tree)

case "x$ERL_TOP" in
  x )
    ;;
  x/* )
    if test -f "$ERL_TOP/erts/emulator/beam/beam_emu.c"
    then
      srcdir="$ERL_TOP"
    else
      AC_MSG_ERROR([Invalid \$ERL_TOP])
    fi
    ;;
  * )
    AC_MSG_ERROR([\$ERL_TOP needs to be absolute]) ;;
esac

dnl How to set srcdir absolute is taken from the GNU Emacs distribution
#### Make srcdir absolute, if it isn't already.  It's important to
#### avoid running the path through pwd unnecessary, since pwd can
#### give you automounter prefixes, which can go away.
case "${srcdir}" in
  /* )
    ;;
  . )
    ## We may be able to use the $PWD environment variable to make this
    ## absolute.  But sometimes PWD is inaccurate.
    ## Make sure CDPATH doesn't affect cd (in case PWD is relative).
    CDPATH=
    if test "x$PWD" != x && test "`(cd "$PWD" ; sh -c pwd)`" = "`pwd`"
    then
      srcdir="$PWD"
    else
      srcdir="`(cd "$srcdir"; pwd)`"
    fi
    ;;
  * )
    srcdir="`(cd "$srcdir"; pwd)`"
    ;;
esac

#
# Now srcdir is absolute and also the top of Erlang distribution, ERL_TOP.
#
if test "x$ERL_TOP" = x
then
  ERL_TOP="$srcdir"
fi

AC_MSG_RESULT([$ERL_TOP])
AC_SUBST(ERL_TOP)

dnl
dnl Aux programs are found in erts/autoconf
dnl
AC_CONFIG_AUX_DIR(${srcdir}/erts/autoconf)

dnl
dnl Figure out what we are running on. And in violation of autoconf
dnl style assume that $host is also what we are building for. I would
dnl like to get cross compiling working, since we actually have
dnl systems we cross compile for!
dnl

case "$host" in
  free_source )
    target_cpu=x86_64
    target_os=unix
    target_vendor=unknown
    target="${target_cpu}-${target_vendor}-${target_os}"
    ;;
  win32 )
    target_cpu=x86_64
    target_os=win32
    target_vendor=unknown
    target="${target_cpu}-${target_vendor}-${target_os}"
    ;;
  * )
    AC_CANONICAL_TARGET
    ;;
esac
TARGET="$target"
AC_SUBST(TARGET)

if test "x$cross_compiling" = xyes
then
  CROSS_COMPILING=yes
else
  CROSS_COMPILING=no
fi
AC_SUBST(CROSS_COMPILING)

dnl Checks for programs.

AC_PROG_CC
AC_PROG_CXX
AC_CHECK_TOOL(LD, [ld])

#
# We need GNU make, complain if we can't find it
#
AC_MSG_CHECKING(for GNU make)
# If there is a Makefile created we don't want make to start making, run
# in a subdirectory and -f /dev/null
MAKE_PROG=x
if test "x$CLEARCASE_MAKE_COMPAT" = xgnu -a "x$CLEARCASE_ROOT" != x
then
  eval clearmake -version 2>&1 | grep clearmake > /dev/null 2>&1
  case $? in
    0 )
      MAKE_PROG="clearmake -V"
      ;;
    * )
      ;;
  esac
fi
if test "$MAKE_PROG" = x
then
  mkdir conftestmake
  if test -d conftestmake
  then
    cd conftestmake
    for m in $MAKE make gmake ggmake
    do
      eval $m --version -f /dev/null 2>&1 | grep GNU > /dev/null 2>&1
      case $? in
        0 )
          MAKE_PROG=$m
          break
          ;;
        * )
          ;;
      esac
    done
    cd ..
  else
    AC_MSG_ERROR(could not create subdirectory)
  fi
fi
rm -rf conftestmake
case $MAKE_PROG in
  x )
    AC_MSG_RESULT(no)
    AC_MSG_ERROR(GNU make is required!)
    ;;
  * )
    AC_MSG_RESULT(yes ($MAKE_PROG))
    AC_SUBST(MAKE_PROG)
    ;;
esac

AC_PROG_INSTALL
if test "x$INSTALL" = "x$ac_aux_dir/install-sh -c" && test -f /usr/ucb/install
then
  case $target_os in
    osf* )
      ;;
    * )
      INSTALL="/usr/ucb/install -c"
      ;;
   esac
fi

AC_PROG_LN_S
AC_PROG_RANLIB

#
# Get erts version from erts/vsn.mk
#
AC_MSG_CHECKING([ERTS version])
[ERTS_VSN=`sed -n "s/^VSN[	 ]*=[	 ]*\(.*\)/\1/p" < $ERL_TOP/erts/vsn.mk`]
AC_MSG_RESULT([$ERTS_VSN])
AC_SUBST(ERTS_VSN)

#
# Get OTP release and OTP version from $ERL_TOP/OTP_VERSION
#
AC_MSG_CHECKING([OTP release])
[OTP_REL=`cat $ERL_TOP/OTP_VERSION | sed "s|\([0-9]*\).*|\1|"`]
AC_MSG_RESULT([$OTP_REL])
AC_SUBST(OTP_REL)

AC_MSG_CHECKING([OTP version])
[OTP_VSN=`cat $ERL_TOP/OTP_VERSION`]
AC_MSG_RESULT([$OTP_VSN])
AC_SUBST(OTP_VSN)

dnl
dnl Options are listed in the order we want them to appear in help.
dnl Many are interdependent, so rearranging them is not to be taken lightly.
dnl

AC_ARG_ENABLE(basho,
AS_HELP_STRING([--enable-basho], [use Basho's production configuration,
  conflicting options cause errors (default)])
AS_HELP_STRING([--disable-basho],
  [allow overrides of Basho's production configuration]),[
  case "$enableval" in
    no )
      enable_basho=no
      ;;
    * )
      enable_basho=yes
      ;;
  esac],enable_basho=yes)

AC_ARG_ENABLE(bootstrap-only,
AS_HELP_STRING([--enable-bootstrap-only],
  [enable bootstrap only configuration]),[
  case "$enableval:$CROSS_COMPILING" in
    no:* )
      BOOTSTRAP_ONLY=no
      ;;
    *:yes )
      AC_MSG_ERROR([Cannot both cross compile and build a bootstrap system])
      ;;
    * )
      BOOTSTRAP_ONLY=yes
      ;;
  esac],BOOTSTRAP_ONLY=no)
AC_SUBST(BOOTSTRAP_ONLY)

AC_ARG_ENABLE(darwin-64bit,
AS_HELP_STRING([--enable-darwin-64bit], [build 64-bit binaries on Darwin
  (default with --enable-basho on OS X)]),[
  case "$enableval:$enable_basho:$target_os" in
    no:yes:darwin* )
      AC_MSG_ERROR([--disable-darwin-64bit not allowed with --enable-basho])
      ;;
    no:* )
      enable_darwin_64bit=no
      ;;
    *:*:darwin* )
      enable_darwin_64bit=yes
      ;;
    * )
      AC_MSG_NOTICE([--enable-darwin-64bit ignored on non-darwin platform])
      enable_darwin_64bit=no
      ;;
  esac])

AC_ARG_ENABLE(m64-build,
AS_HELP_STRING([--enable-m64-build], [build 64-bit binaries using the -m64
  flag to the compiler and linker (default with --enable-basho)]),[
  case "$enableval:$enable_basho:$target_os" in
    no:yes:* )
      AC_MSG_ERROR([--disable-m64-build not allowed with --enable-basho])
      ;;
    no:* )
      if test x$enable_darwin_64bit = xyes
      then
        AC_MSG_ERROR([--disable-m64-build conflicts with --enable-darwin-64bit])
      fi
      enable_m64_build=no
      ;;
    *:darwin* )
      enable_m64_build=yes
      enable_darwin_64bit=yes
      ;;
    * )
      enable_m64_build=yes
      ;;
  esac],[if test x$enable_darwin_64bit = xyes
  then
    enable_m64_build=yes
  fi])

AC_ARG_ENABLE(darwin-universal,
AS_HELP_STRING([--enable-darwin-universal], [build 32-bit universal binaries
  on Darwin. Note: this release probably won't build with this flag, it is
  included for compatibility only!]),[
  case "$enableval:$enable_basho:$target_os" in
    no:* )
      enable_darwin_universal=no
      ;;
    *:yes:* )
      AC_MSG_ERROR([--enable-darwin-universal not allowed with --enable-basho])
      ;;
    *:no:darwin* )
      if test x$enable_darwin_64bit = xyes
      then
        AC_MSG_ERROR([--enable-darwin-universal not supported for 64-bit build])
      fi
      case "$build_os:$build_cpu" in
        darwin*:x86_64 | darwin*:i386 )
          enable_darwin_univeral=yes
          ;;
        * )
          AC_MSG_ERROR(
            [--enable-darwin-universal only supported on Darwin x86 hosts])
          ;;
      esac
      ;;
    * )
      AC_MSG_NOTICE([--enable-darwin-universal ignored on non-darwin platform])
      enable_darwin_univeral=no
      ;;
  esac],enable_darwin_universal=no)

AC_ARG_ENABLE(m32-build,
AS_HELP_STRING([--enable-m32-build], [build 32-bit binaries using the -m32
  flag to the compiler and linker. Note: this release probably won't build
  with this flag, it is included for compatibility only!]),[
  case "$enableval:$enable_basho" in
    no:* )
      enable_m32_build=no
      ;;
    *:yes )
      AC_MSG_ERROR([--enable-m32-build not allowed with --enable-basho])
      ;;
    * )
      if test x$enable_m64_build = xyes
      then
        AC_MSG_ERROR([--enable-m32-build conflicts with 64-bit build])
      fi
      enable_m32_build=yes
      ;;
  esac],[if test x$enable_darwin_universal = xyes
  then
    enable_m32_build=yes
  fi])

if test x$enable_m32_build = x -a  x$enable_m64_build = x
then
  case "$target_cpu" in
    x86_64 )
      enable_m64_build=yes
      ;;
    i?86 | ia32 )
      enable_m32_build=yes
      ;;
  esac
fi

AC_ARG_ENABLE(smp-support,
AS_HELP_STRING([--enable-smp-support], [enable smp support (default)])
AS_HELP_STRING([--disable-smp-support], [disable smp support]),[
  case "$enableval:$enable_basho" in
    no:yes )
      AC_MSG_ERROR([--disable-smp-support not allowed with --enable-basho])
      ;;
    no:* )
      enable_smp_support=no
      ;;
    * )
      enable_smp_support=yes
      ;;
  esac],enable_smp_support=yes)

AC_ARG_ENABLE(threads,
AS_HELP_STRING([--enable-threads], [enable async thread support (default)])
AS_HELP_STRING([--disable-threads], [disable async thread support]),[
  case "$enableval:$enable_basho" in
    no:yes )
      AC_MSG_ERROR([--disable-threads not allowed with --enable-basho])
      ;;
    no:* )
      enable_threads=no
      ;;
    * )
      enable_threads=yes
      ;;
  esac],enable_threads=yes)

AC_ARG_ENABLE(kernel-poll,
AS_HELP_STRING([--enable-kernel-poll], [enable kernel poll support (default)])
AS_HELP_STRING([--disable-kernel-poll], [disable kernel poll support]),[
  case "$enableval:$enable_basho" in
    no:yes )
      AC_MSG_ERROR([--disable-kernel-poll not allowed with --enable-basho])
      ;;
    no:* )
      enable_kernel_poll=no
      ;;
    * )
      enable_kernel_poll=yes
      ;;
  esac],enable_kernel_poll=yes)

AC_ARG_ENABLE(hipe,
AS_HELP_STRING([--enable-hipe], [enable HiPE support])
AS_HELP_STRING([--disable-hipe], [disable HiPE support (default)]),[
  case "$enableval:$enable_basho" in
    no:* )
      enable_hipe=no
      ;;
    *:yes )
      AC_MSG_ERROR([--enable-hipe not allowed on \$target with --enable-basho])
      ;;
    * )
      enable_hipe=yes
      ;;
  esac],enable_hipe=no)

AC_ARG_ENABLE(native-libs,
AS_HELP_STRING([--enable-native-libs],
  [compile Erlang libraries to native code]),[
  case "$enableval:$enable_hipe" in
    no:* )
      NATIVE_LIBS_ENABLED=
      ;;
    *:yes )
      NATIVE_LIBS_ENABLED=yes
      ;;
    * )
      AC_MSG_NOTICE([--enable-native-libs ignored with HiPE disabled])
      NATIVE_LIBS_ENABLED=
      ;;
  esac],NATIVE_LIBS_ENABLED=)
AC_SUBST(NATIVE_LIBS_ENABLED)

AC_ARG_ENABLE(ttod,
AS_HELP_STRING([--enable-ttod], [enables the tolerant_timeofday (TTOD)
  mechanism, by which ERTS attempts to smooth sudden changes of system time
  (default). When built, TTOD can be disabled at runtime with the '+c'
  emulator switch.])
AS_HELP_STRING([--disable-ttod], [disables the TTOD mechanism entirely, which
  is strongly discouraged.])
AS_HELP_STRING([--enable-ttod=@<:@comma-separated list of strategies@:>@],
 [enables a specific set of TTOD strategies, possibly documented elsewhere.
  Note: this option should ONLY be used with expert guidance!])
AS_HELP_STRING([--disable-ttod=@<:@comma-separated list of strategies@:>@],
 [disables a specific set of TTOD strategies.
  Note: this option should ONLY be used with expert guidance!]))

AC_ARG_ENABLE(dirty-schedulers,
AS_HELP_STRING([--enable-dirty-schedulers], [enable dirty scheduler support.
  Note: dirty schedulers are an EXPERIMENTAL feature!]))

AC_ARG_ENABLE(halfword-emulator,
AS_HELP_STRING([--enable-halfword-emulator], [enable halfword emulator
  (only for 64bit builds). Note: Halfword emulator is marked as deprecated
  and scheduled for removal in future major release.]),[
  case "$enableval:$enable_basho" in
    no:* )
      enable_halfword_emulator=no
      ;;
    *:yes )
      AC_MSG_ERROR([--enable-halfword-emulator not allowed with --enable-basho])
      ;;
    * )
      enable_halfword_emulator=yes
      ;;
  esac],enable_halfword_emulator=no)

AC_ARG_WITH(termcap,
AS_HELP_STRING([--with-termcap], [use termcap (default)])
AS_HELP_STRING([--without-termcap],
  [do not use any termcap libraries (ncurses,curses,termcap,termlib)]))

AC_ARG_ENABLE(sctp,
AS_HELP_STRING([--enable-sctp], [enable sctp support])
AS_HELP_STRING([--disable-sctp], [disable sctp support]))

AC_ARG_WITH(dynamic-trace,
AS_HELP_STRING([--with-dynamic-trace={dtrace|systemtap}],
  [specify use of dynamic trace framework, dtrace or systemtap])
AS_HELP_STRING([--without-dynamic-trace],
  [don't enable any dynamic tracing (default)]))

AC_ARG_ENABLE(vm-probes,
AS_HELP_STRING([--enable-vm-probes],
  [add dynamic trace probes to the Beam VM (only possible if
   --with-dynamic-trace is enabled, and then default)]))

AC_ARG_WITH(javac,
AS_HELP_STRING([--with-javac=JAVAC], [specify Java compiler to use])
AS_HELP_STRING([--with-javac], [use a Java compiler if found (default)])
AS_HELP_STRING([--without-javac], [don't use any Java compiler]))

AC_ARG_ENABLE(megaco_flex_scanner_lineno,
AS_HELP_STRING([--disable-megaco-flex-scanner-lineno],
  [disable megaco flex scanner lineno]))

AC_ARG_ENABLE(megaco_reentrant_flex_scanner,
AS_HELP_STRING([--disable-megaco-reentrant-flex-scanner],
  [disable reentrant megaco flex scanner]))

AC_ARG_WITH(ssl,
AS_HELP_STRING([--with-ssl], [use SSL (default)])
AS_HELP_STRING([--with-ssl=PATH], [specify location of OpenSSL include and lib])
AS_HELP_STRING([--without-ssl], [don't use SSL]),[
  case "$enableval:$enable_basho" in
    no:yes )
      AC_MSG_ERROR([--without-ssl not allowed with --enable-basho])
      ;;
    no:* )
      with_ssl=no
      ;;
    * )
      with_ssl="$enableval"
      ;;
  esac],with_ssl=yes)

AC_ARG_WITH(ssl-incl,
AS_HELP_STRING([--with-ssl-incl=PATH],
  [location of OpenSSL include dir, if different than specified by
   --with-ssl=PATH]))

AC_ARG_WITH(ssl-rpath,
AS_HELP_STRING([--with-ssl-rpath=yes|no|PATHS],
  [runtime library path for OpenSSL. Default is 'yes', which equates to a
   number of standard locations. If 'no', then no runtime library paths wil
   be used. Anything else should be a comma separated list of paths.]))

AC_ARG_ENABLE(dynamic-ssl-lib,
AS_HELP_STRING([--disable-dynamic-ssl-lib],
  [disable using dynamic openssl libraries]))

AC_ARG_ENABLE(builtin-zlib,
AS_HELP_STRING([--enable-builtin-zlib],[force use of our own built-in zlib]))

dnl This functionality has been lost along the way... :(
dnl It could perhaps be nice to reintroduce some day; therefore,
dnl it is not removed just commented out.
dnl
dnl #
dnl # Set Erlang man page directory
dnl #
dnl AC_ARG_ENABLE(erlang-mandir,
dnl [  --disable-erlang-mandir do not install Erlang man pages in a private directory],
dnl [ case "$enableval" in
dnl     no) erl_mandir=$mandir ;;
dnl     *)  erl_mandir='$(erlang_libdir)/man' ;;
dnl   esac ], erl_mandir='$(erlang_libdir)/man')
dnl AC_SUBST(erl_mandir)

AC_ARG_WITH(libatomic_ops,
AS_HELP_STRING([--with-libatomic_ops=PATH],
  [specify and prefer usage of libatomic_ops in the ethread library]))

m4_define(DEFAULT_SANITIZERS, [address,undefined])
AC_ARG_ENABLE(sanitizers,
AS_HELP_STRING([--enable-sanitizers=@<:@comma-separated list of sanitizers@:>@],
  [Default=DEFAULT_SANITIZERS]))

AC_ARG_ENABLE([silent-rules],
AS_HELP_STRING([--enable-silent-rules],
  [less verbose build output (default) (undo: "make V=1")])
AS_HELP_STRING([--disable-silent-rules],
  [verbose build output (undo: "make V=0")]),[
  case "$enableval" in
    no )
      DEFAULT_VERBOSITY=1
      ;;
    * )
      DEFAULT_VERBOSITY=0
      ;;
  esac],DEFAULT_VERBOSITY=0)
AC_SUBST(DEFAULT_VERBOSITY)

dnl
dnl Long and ugly, but it maintains platform compatibility
dnl
tmp_var_list="CFLAGS LDFLAGS"
if test "x$CXXFLAGS" != x
then
  tmp_var_list="$tmp_var_list CXXFLAGS"
fi
if test x$enable_m32_build = xyes
then
  if test x$enable_darwin_univeral = xyes
  then
    for tmp_var in $tmp_var_list
    do
      eval "tmp_val=\$$tmp_var"
      tmp_str=`echo ' ' $tmp_val | tr -s ' '`
      for tmp_arg in '-arch ppc' '-arch i386'
      do
        case " $tmp_str " in
          *" $tmp_arg "* )
            ;;
          * )
            eval "$tmp_var=\"$tmp_arg \$$tmp_var\""
            export $tmp_var
            ;;
        esac
      done
    done
  fi
  for tmp_var in $tmp_var_list
  do
    eval "tmp_val=\$$tmp_var"
    tmp_str=`echo ' ' $tmp_val | tr -s ' '`
    tmp_args='-m32'
    # maintain the pattern for consistency, even with only one arg
    for tmp_arg in $tmp_args
    do
      case " $tmp_str " in
        *" $tmp_arg "* )
          ;;
        * )
          eval "$tmp_var=\"$tmp_arg \$$tmp_var\""
          export $tmp_var
          ;;
      esac
    done
  done
elif test x$enable_m64_build = xyes
then
  for tmp_var in $tmp_var_list
  do
    eval "tmp_val=\$$tmp_var"
    tmp_str=`echo ' ' $tmp_val | tr -s ' '`
    tmp_args='-m64'
    if test "x$target_cpu" = xx86_64
    then
      tmp_args="-mcx16 $tmp_args"
    fi
    for tmp_arg in $tmp_args
    do
      case " $tmp_str " in
        *" $tmp_arg "* )
          ;;
        * )
          eval "$tmp_var=\"$tmp_arg \$$tmp_var\""
          export $tmp_var
          ;;
      esac
    done
  done
fi

rm -f $ERL_TOP/lib/SKIP-APPLICATIONS
for app in `cd lib && ls -d *`; do
  var=`eval echo \\$with_$app`
  if test X${var} = Xno; then
    echo "$app" >> $ERL_TOP/lib/SKIP-APPLICATIONS
  fi
done

export ERL_TOP
AC_CONFIG_SUBDIRS(lib erts)

AC_CONFIG_FILES([Makefile make/output.mk])
AC_CONFIG_FILES([make/emd2exml], [chmod +x make/emd2exml])

AC_OUTPUT

pattern="lib/*/SKIP"
files=`echo $pattern`
if test "$files" != "$pattern" || test -f $ERL_TOP/lib/SKIP-APPLICATIONS; then
  echo '*********************************************************************'
  echo '**********************  APPLICATIONS DISABLED  **********************'
  echo '*********************************************************************'
  echo
  if test "$files" != "$pattern"; then
    for skipfile in $files; do
      app=`dirname $skipfile`; app=`basename $app`
      printf "%-15s: " $app; cat $skipfile
    done
  fi
  if test -f $ERL_TOP/lib/SKIP-APPLICATIONS; then
    for skipapp in `cat $ERL_TOP/lib/SKIP-APPLICATIONS`; do
      printf "%-15s: User gave --without-%s option\n" $skipapp $skipapp
    done
  fi
  echo
  echo '*********************************************************************'
fi
pattern="lib/*/CONF_INFO"
files=`echo $pattern`
if test "$files" != "$pattern" || test -f erts/CONF_INFO; then
  echo '*********************************************************************'
  echo '**********************  APPLICATIONS INFORMATION  *******************'
  echo '*********************************************************************'
  echo
  test "$files" != "$pattern" || files=""
  test ! -f erts/CONF_INFO || files="$files erts/CONF_INFO"
  for infofile in $files; do
    app=`dirname $infofile`; app=`basename $app`
    printf "%-15s: " $app; cat $infofile
  done
  echo
  echo '*********************************************************************'
fi
if test -f "erts/doc/CONF_INFO"; then
  echo '*********************************************************************'
  echo '**********************  DOCUMENTATION INFORMATION  ******************'
  echo '*********************************************************************'
  echo
  printf "%-15s: \n" documentation;
  havexsltproc="yes"
  for cmd in `cat erts/doc/CONF_INFO`; do
    echo "                 $cmd is missing."
  if test $cmd = "xsltproc"; then
    havexsltproc="no"
  fi
  done
  if test $havexsltproc = "no"; then
    echo '                 The documentation can not be built.'
  else
    echo '                 Using fakefop to generate placeholder PDF files.'
  fi
  echo
  echo '*********************************************************************'
fi
